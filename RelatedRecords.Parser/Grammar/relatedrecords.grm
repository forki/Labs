"Name"     = 'Related Records Grammar'
"Author"   = 'Miguel Figueroa'
"Version"  = '1.0'
"About"    = 'Commands for Related Records'

"Start Symbol" = <Start>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------
{WS}           = {Whitespace} - {CR} - {LF}
{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["]
{Quoted String Chars} = {Printable} + {HT} - ['']
{Bracket String Chars} = {Printable} + {HT} - ['['] - [']']
! -------------------------------------------------
! Terminals
! -------------------------------------------------
Whitespace    = {WS}+
NewLine       = {CR}{LF} | {CR} | {LF}
StringLiteral = '"' {String Chars}* '"'
              | '''' {Quoted String Chars}* ''''
              | '[' {Bracket String Chars}* ']'
Identifier    = {ID Head}{ID Tail}*
Integer       = {Digit} + {Digit}*
Decimal       = {Digit} + {Digit}* [.] {Digit}* 
! -------------------------------------------------
! Rules
! -------------------------------------------------
! The grammar starts below
<nl>     ::= NewLine <nl>          !One or more
          |  NewLine

<nl Opt> ::= NewLine <nl Opt>      !Zero or more
          |  !Empty

! <nl opt> removes blank lines before first statement
<Start>   ::= <nl opt> <CommandExp> <nl Opt>

<CommandExp> ::= <ImportExp>
              | <CloneExp>
              | <RemoveExp>
              | <RefreshExp>
              | <LoadExp>
              | <TableExp>
              | <RelateExp>
              | <UnrelateExp>
              | <ExportExp>
              | <BackExp>
              | <RootExp>
              | <TablesExp>
              | <CatalogsExp>
              | <ColumnsExp>
              | <TopnExp>
              | <ChildExp>
              | <HelpExp>
              | <QueryExp>
              | <RunQueryExp>
              | <TransformExp>
              
! import catalog <catalogname> [server <servername> [user <username> password <password>]]
<ImportExp>   ::= import <CatalogExp> !using localhost and trusted creds
               | import <CatalogExp> <UserExp> <PasswordExp> !using localhost
               | import <CatalogExp> <ServerExp> <UserExp> <PasswordExp>

! clone [catalog <catalogname>] [as <new-catalogname>]
<CloneExp>    ::= clone !current catalog
               | clone <CatalogExp> !catalog as new default name
               | clone <AsExp> !current catalog
               | clone <CatalogExp> <AsExp>

! remove [catalog <catalogname>]
<RemoveExp>   ::= remove !current catalog
               | remove <CatalogExp>

! remove [catalog <catalogname>]
<RefreshExp>   ::= refresh !current catalog
               | refresh <CatalogExp>

! load [catalog <catalogname>] 
<LoadExp>     ::= load !default catalog
               | load <CatalogExp>
               | load <CatalogExp> default

! table <tablename> [default] [where <column> ((<operator> <value>) | (is [not] NULL) | (between <value1> and <value2>)]
<TableExp>    ::= table !default table
               | table StringLiteral
               | table StringLiteral default
               | table StringLiteral default where <Expression> 
               | table StringLiteral where <Expression> 

! relate [<source-tablename>] to <target-tablename> on <source-columnname> = <target-columnname>
<RelateExp>   ::= relate to StringLiteral on StringLiteral '=' StringLiteral 
               | relate StringLiteral to StringLiteral on StringLiteral '=' StringLiteral
               
! unrelate [<source-tablename>] to <target-tablename>
<UnrelateExp> ::= unrelate to StringLiteral
               | unrelate StringLiteral to StringLiteral
               
! export [<tablename>] as (sql | html | json | xml)
<ExportExp>   ::= export as sql
               | export as html
               | export as json
               | export as xml
               | export StringLiteral as sql
               | export StringLiteral as html
               | export StringLiteral as json
               | export StringLiteral as xml
               | export as sql nochild
               | export as html nochild
               | export as json nochild
               | export as xml nochild
               | export StringLiteral as sql nochild
               | export StringLiteral as html nochild
               | export StringLiteral as json nochild
               | export StringLiteral as xml nochild

! back
<BackExp>    ::= back
              
! root
<RootExp>    ::= home

! tables
<TablesExp>  ::= tables
              | tables Integer

! cataglos
<CatalogsExp> ::= catalogs
               | catalogs Integer              
! columns
<ColumnsExp> ::= columns
              | columns Integer

! top n
<TopnExp>     ::= top Integer

!child 
<ChildExp>    ::= child
               | child Integer
               | child StringLiteral

<CatalogExp>  ::= catalog StringLiteral
<UserExp>     ::= user StringLiteral
<PasswordExp> ::= password StringLiteral
<ServerExp>   ::= server StringLiteral
<AsExp>       ::= as StringLiteral
<HelpExp>     ::= help

<QueryExp>    ::= query StringLiteral
               | query StringLiteral row Integer

<TransformExp> ::= transform
                | transform template StringLiteral
                | transform StringLiteral
                | transform StringLiteral template StringLiteral

<RunQueryExp> ::= run StringLiteral
               | run StringLiteral with <ParamExpression>
               | run StringLiteral with <ParamExpression> ',' <ParamExpression2>
               | run StringLiteral with <ParamExpression> ',' <ParamExpression2> ',' <ParamExpression3>
               | run StringLiteral with <ParamExpression> ',' <ParamExpression2> ',' <ParamExpression3> ',' <ParamExpression4>
               | run StringLiteral with <ParamExpression> ',' <ParamExpression2> ',' <ParamExpression3> ',' <ParamExpression4> ',' <ParamExpression5>

<ParamExpression> ::= <ParamExpression> '=' <ParamValue>
               | <ParamValue>

<ParamExpression2> ::= <ParamExpression2> '=' <ParamValue>
               | <ParamValue>

<ParamExpression3> ::= <ParamExpression3> '=' <ParamValue>
               | <ParamValue>

<ParamExpression4> ::= <ParamExpression4> '=' <ParamValue>
               | <ParamValue>

<ParamExpression5> ::= <ParamExpression5> '=' <ParamValue>
               | <ParamValue>

<ParamValue>  ::= '-' <PValue>
               | <PValue>
               
<PValue>      ::= Identifier
               | null
               | Integer
               | Decimal
               | StringLiteral
                              
<Expression>  ::= <Expression> '>'  <Negate Exp> 
               |  <Expression> '<'  <Negate Exp>
               |  <Expression> '<=' <Negate Exp>
               |  <Expression> '>=' <Negate Exp>
               |  <Expression> '=' <Negate Exp>    !Equal
               |  <Expression> '<>' <Negate Exp>    !Not equal
               |  <Expression> is <Negate Exp>
               |  <Expression> not between <Negate Exp> and <Negate Exp>
               |  <Expression> between <Negate Exp> and <Negate Exp>
               |  <Expression> like StringLiteral
               |  <Negate Exp>

<Negate Exp>  ::= '-' <Value> 
               | not <Value>
               | <Value> 

!Add more values to the rule below - as needed

<Value>       ::= Identifier
               |  '(' <Expression> ')'
               | null
               | Integer
               | Decimal
               | StringLiteral
